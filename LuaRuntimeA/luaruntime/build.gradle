apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1001
        versionName '1.01'

        externalNativeBuild {
            ndkBuild {
                arguments "NDK_APPLICATION_MK:=jni/Application.mk", "APP_PLATFORM:=android-21", "-j8"
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['jni/']
        }
    }
    externalNativeBuild {
        ndkBuild {
            path "jni/Android.mk"
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            jniDebuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    android.libraryVariants.all { variant ->
        def version_name = "${defaultConfig.versionName}".replace(".", "")
        def file_name = "luaruntime_${version_name}_${defaultConfig.versionCode}.aar"
        if (variant.buildType.isDebuggable()) {
            file_name = "luaruntime_${version_name}_${defaultConfig.versionCode}_d.aar"
        }

        variant.outputs.each { output ->
            output.outputFileName = "luaruntime.aar";
        }

        // 复制aar到指定目录
        variant.assemble.doLast {
            variant.outputs.all { output ->

                def outputPath = [project.rootDir.absolutePath, "dist"].join(File.separator)
                copy {
                    from output.outputFile
                    into outputPath
                    rename { file_name }
                }
                println("Copying : ${output.outputFile} -> ${outputFile}")
            }
        }

    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
}
